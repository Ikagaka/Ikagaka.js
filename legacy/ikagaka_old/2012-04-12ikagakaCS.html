<html lang="ja">
<head>
<meta charset="utf-8" />
<meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
<meta name="viewport" content="width=700" />
<meta name="apple-mobile-web-app-capable" content="yes" />
<meta name="apple-mobile-web-app-status-bar-style" content="black-translucent" />
<title>如何か</title>
<link rel="stylesheet" href="./normalize.css" />
<script src="./jquery-1.7.1.min.js"></script>
<script src="./coffee-script.js"></script>
<script type="text/javascript">
if (!Function.prototype.bind) {  
  Function.prototype.bind = function (oThis) {  
    if (typeof this !== "function") {  
      // closest thing possible to the ECMAScript 5 internal IsCallable function  
      throw new TypeError("Function.prototype.bind - what is trying to be bound is not callable");  
    }  
  
    var aArgs = Array.prototype.slice.call(arguments, 1),   
        fToBind = this,   
        fNOP = function () {},  
        fBound = function () {  
          return fToBind.apply(this instanceof fNOP  
                                 ? this  
                                 : oThis || window,  
                               aArgs.concat(Array.prototype.slice.call(arguments)));  
        };  
  
    fNOP.prototype = this.prototype;  
    fBound.prototype = new fNOP();  
  
    return fBound;  
  };  
}  
</script>
<script type="text/coffeescript">
exice_zJson =
	shell:
		master:
			surface:
				"0":
					"element":[
						["base","./exice_z/shell/master/surface0000.png"]]
					"collision":[
						[80,62,143,95,"Head"]
						[91,111,139,142,"Face"]
						[89,181,140,216,"Bust"]
						[184,187,211,238,"LeftST"]]
					"animation":[
						{"timing":"sometimes"
						"pattern":[
							["overlay",61,5,85,100]
							["overlay",60,5,85,100]
							["overlay",-1,5]]}]
				"1":
					"element":[
						["base","./exice_z/shell/master/surface0001.png"]]
					"collision":[
						[94,73,156,106,"Head"]
						[102,110,154,141,"Face"]
						[109,176,146,212,"Bust"]
						[184,213,208,281,"LeftST"]]
				"2":
					"element":[
						["base","./exice_z/shell/master/surface0002.png"]]
					"collision":[
						[73,68,122,102,"Head"]
						[82,106,129,145,"Face"]
						[99,161,149,202,"Bust"]
						[179,161,213,239,"LeftST"]]
				"3":
					"element":[
						["base","./exice_z/shell/master/surface0003.png"]]
					"collision":[
						[85,104,134,138,"Head"]
						[89,144,138,174,"Face"]
						[107,191,149,221,"Bust"]
						[175,175,209,246,"LeftST"]]
				"4":
					"element":[
						["base","./exice_z/shell/master/surface0004.png"]]
					"collision":[
						[86,97,156,128,"Head"]
						[108,136,157,168,"Face"]
						[117,187,154,221,"Bust"]
						[202,230,245,303,"LeftST"]]
				"5":
					"element":[
						["base","./exice_z/shell/master/surface0005.png"]]
					"collision":[
						[95,67,157,103,"Head"]
						[104,116,158,143,"Face"]
						[106,174,155,209,"Bust"]
						[182,189,209,274,"LeftST"]]
				"6":
					"element":[
						["base","./exice_z/shell/master/surface0006.png"]]
					"collision":[
						[95,67,157,103,"Head"]
						[104,116,158,143,"Face"]
						[106,174,155,209,"Bust"]
						[182,189,209,274,"LeftST"]]
				"7":
					"element":[
						["base","./exice_z/shell/master/surface0007.png"]]
					"collision":[
						[86,61,144,101,"Head"]
						[90,107,146,145,"Face"]
						[87,178,140,200,"Bust"]
						[181,164,211,240,"LeftST"]]
				"8":
					"element":[
						["base","./exice_z/shell/master/surface0008.png"]]
					"collision":[
						[80,62,143,95,"Head"]
						[91,111,139,142,"Face"]
						[89,181,140,216,"Bust"]
						[184,187,211,238,"LeftST"]]
				"9":
					"element":[
						["base","./exice_z/shell/master/surface0009.png"]]
					"collision":[
						[80,62,143,95,"Head"]
						[91,111,139,142,"Face"]
						[89,181,140,216,"Bust"]
						[184,187,211,238,"LeftST"]]
				"10":
					"element":[
						["base","./exice_z/shell/master/surface0010.png"]]
				"11":
					"element":[
						["base","./exice_z/shell/master/surface0011.png"]]
				"12":
					"element":[
						["base","./exice_z/shell/master/surface0012.png"]]
				"13":
					"element":[
						["base","./exice_z/shell/master/surface0013.png"]]
				"14":
					"element":[
						["base","./exice_z/shell/master/surface0014.png"]]
				"15":
					"element":[
						["base","./exice_z/shell/master/surface0015.png"]]
					"animation":[
						{"timing":"runonce"
						"pattern":[
							["overlay",16,20,0,0]
							["overlay",16,14,0,5]
							["overlay",16,12,0,10]
							["overlay",16,10,0,15]
							["overlay",16,8,0,20]
							["overlay",16,5,0,35]
							["overlay",16,5,0,40]
							["overlay",16,5,0,60]
							["overlay",16,5,0,80]
							["overlay",16,5,0,100]]}]
				"16":
					"element":[
						["base","./exice_z/shell/master/surface0016.png"]]
				"17":
					"element":[
						["base","./exice_z/shell/master/surface0017.png"]]
				"18":
					"element":[
						["base","./exice_z/shell/master/surface0018.png"]]
					"collision":[
						[11,0,121,185,"Brade1"]]
				"19":
					"element":[
						["base","./exice_z/shell/master/surface0019.png"]]
					"collision":[
						[11,0,121,185,"Brade1"]]
				"20":
					"element":[
						["base","./exice_z/shell/master/surface0020.png"]]
					"collision":[
						[8,133,109,350,"Brade0"]]
				"21":
					"element":[
						["base","./exice_z/shell/master/surface0021.png"]]
					"collision":[
						[8,133,109,350,"Brade0"]]
				"22":
					"element":[
						["base","./exice_z/shell/master/surface0022.png"]]
					"collision":[
						[95,67,157,103,"Head"]
						[104,116,158,143,"Face"]
						[106,174,155,209,"Bust"]
						[182,189,209,274,"LeftST"]]
					"animation":[
						{"timing":"sometimes"
						"pattern":[
							["overlay",63,5,100,105]
							["overlay",62,5,100,105]
							["overlay",-1,6]]}]
				"23":
					"element":[
						["base","./exice_z/shell/master/surface0023.png"]]
					"collision":[
						[91,62,148,95,"Head"]
						[97,103,141,143,"Face"]
						[90,163,138,198,"Bust"]
						[165,168,195,248,"LeftST"]]
					"animation":[
						{"timing":"sometimes"
						"pattern":[
							["overlay",65,5,90,100]
							["overlay",64,5,90,100]
							["overlay",-1,6]]}]
				"24":
					"element":[
						["base","./exice_z/shell/master/surface0024.png"]]
					"collision":[
						[87,64,148,100,"Head"]
						[95,111,141,143,"Face"]
						[92,161,144,203,"Bust"]
						[173,157,207,238,"LeftST"]]
				"25":
					"element":[
						["base","./exice_z/shell/master/surface0025.png"]]
					"collision":[
						[94,73,156,106,"Head"]
						[102,110,154,141,"Face"]
						[109,176,146,212,"Bust"]
						[184,213,208,281,"LeftST"]]
				"26":
					"element":[
						["base","./exice_z/shell/master/surface0026.png"]]
					"collision":[
						[73,68,122,102,"Head"]
						[82,106,129,145,"Face"]
						[99,161,149,202,"Bust"]
						[179,161,213,239,"LeftST"]]
				"27":
					"element":[
						["base","./exice_z/shell/master/surface0027.png"]]
				"28":
					"element":[
						["base","./exice_z/shell/master/surface0028.png"]]
					"collision":[
						[87,64,148,100,"Head"]
						[95,111,141,143,"Face"]
						[92,161,144,203,"Bust"]
						[173,157,207,238,"LeftST"]]
				"29":
					"element":[
						["base","./exice_z/shell/master/surface0029.png"]]
					"collision":[
						[86,61,144,101,"Head"]
						[90,107,146,145,"Face"]
						[87,178,140,200,"Bust"]
						[181,164,211,240,"LeftST"]]
				"30":
					"element":[
						["base","./exice_z/shell/master/surface0030.png"]]
					"collision":[
						[85,79,150,105,"HeadEX"]
						[94,110,140,138,"FaceEX"]
						[96,168,139,191,"BustEX"]
						[154,294,177,341,"LeftST"]]
				"31":
					"element":[
						["base","./exice_z/shell/master/surface0031.png"]]
					"collision":[
						[85,79,150,105,"HeadEX"]
						[94,110,140,138,"FaceEX"]
						[96,168,139,191,"BustEX"]
						[154,294,177,341,"LeftST"]]
				"32":
					"element":[
						["base","./exice_z/shell/master/surface0032.png"]]
					"collision":[
						[85,79,150,105,"HeadEX"]
						[94,110,140,138,"FaceEX"]
						[96,168,139,191,"BustEX"]
						[154,294,177,341,"LeftST"]]
				"33":
					"element":[
						["base","./exice_z/shell/master/surface0033.png"]]
					"collision":[
						[80,62,143,95,"Head"]
						[91,111,139,142,"Face"]
						[89,181,140,216,"Bust"]
						[184,187,211,238,"LeftST"]]
				"34":
					"element":[
						["base","./exice_z/shell/master/surface0034.png"]]
					"collision":[
						[95,67,157,103,"Head"]
						[104,116,158,143,"Face"]
						[106,174,155,209,"Bust"]
						[182,189,209,274,"LeftST"]]
				"35":
					"element":[
						["base","./exice_z/shell/master/surface0035.png"]]
					"collision":[
						[80,62,143,95,"Head"]
						[91,111,139,142,"Face"]
						[89,181,140,216,"Bust"]
						[184,187,211,238,"LeftST"]]
				"36":
					"element":[
						["base","./exice_z/shell/master/surface0036.png"]]
					"collision":[
						[98,65,155,100,"Head"]
						[100,110,151,141,"Face"]
						[97,176,151,211,"Bust"]
						[190,237,216,304,"LeftST"]]
					"animation":[
						{"timing":"sometimes"
						"pattern":[
							["overlay",37,8,0,230]
							["overlay",38,8,0,230]
							["overlay",39,8,0,230]
							["overlay",37,8,0,230]
							["overlay",38,8,0,230]
							["overlay",39,8,0,230]
							["overlay",-1,8,0,0]]}
						{"timing":"sometimes"
						"pattern":[
							["overlay",37,6,0,230]
							["overlay",38,6,0,230]
							["overlay",39,6,0,230]
							["overlay",37,6,0,230]
							["overlay",38,6,0,230]
							["overlay",39,6,0,230]
							["overlay",-1,6,0,0]]}
						{"timing":"sometimes"
						"pattern":[
							["overlay",37,8,0,230]
							["overlay",38,8,0,230]
							["overlay",39,8,0,230]
							["overlay",37,8,0,230]
							["overlay",38,8,0,230]
							["overlay",39,8,0,230]
							["overlay",-1,8,0,0]]}
						{"timing":"sometimes"
						"pattern":[
							["overlay",37,10,0,230]
							["overlay",38,10,0,230]
							["overlay",39,10,0,230]
							["overlay",37,10,0,230]
							["overlay",38,10,0,230]
							["overlay",39,10,0,230]
							["overlay",-1,10,0,0]]}]
				"37":
					"element":[
						["base","./exice_z/shell/master/surface0037.png"]]
				"38":
					"element":[
						["base","./exice_z/shell/master/surface0038.png"]]
				"39":
					"element":[
						["base","./exice_z/shell/master/surface0039.png"]]
				"40":
					"element":[
						["base","./exice_z/shell/master/surface0040.png"]]
					"collision":[
						[80,62,143,95,"Head"]
						[91,111,139,142,"Face"]
						[89,181,140,216,"Bust"]
						[184,187,211,238,"LeftST"]]
					"animation":[
						{"timing":"runonce"
						"pattern":[
							["overlay",45,8]
							["overlay",46,8]
							["overlay",47,8]
							["overlay",48,8]]}]
				"41":
					"element":[
						["base","./exice_z/shell/master/surface0041.png"]]
					"collision":[
						[80,62,143,95,"Head"]
						[91,111,139,142,"Face"]
						[89,181,140,216,"Bust"]
						[184,187,211,238,"LeftST"]]
					"animation":[
						{"timing":"runonce"
						"pattern":[
							["overlay",48,8]
							["overlay",47,8]
							["overlay",46,8]
							["overlay",45,8]
							["overlay",43,8]
							["overlay",-1,8]]}]
				"42":
					"element":[
						["base","./exice_z/shell/master/surface0042.png"]]
					"animation":[
						{"timing":"runonce"
						"pattern":[
							["base",81,8]
							["base",82,8]
							["base",83,8]
							["base",84,8]
							["base",85,8]
							["base",86,8]
							["base",87,8]
							["base",88,8]
							["base",89,8]
							["base",43,8]]}]
				"43":
					"element":[
						["base","./exice_z/shell/master/surface0043.png"]]
					"animation":[
						{"timing":"runonce"
						"pattern":[
							["base",89,8]
							["base",88,8]
							["base",87,8]
							["base",86,8]
							["base",85,8]
							["base",84,8]
							["base",83,8]
							["base",82,8]
							["base",81,8]
							["base",42,8]]}]
				"45":
					"element":[
						["base","./exice_z/shell/master/surface0045.png"]]
				"46":
					"element":[
						["base","./exice_z/shell/master/surface0046.png"]]
				"47":
					"element":[
						["base","./exice_z/shell/master/surface0047.png"]]
				"48":
					"element":[
						["base","./exice_z/shell/master/surface0048.png"]]
				"50":
					"element":[
						["base","./exice_z/shell/master/surface0050.png"]]
					"animation":[
						{"timing":"runonce"
						"pattern":[
							["base",52,6]
							["base",53,6]
							["base",54,6]
							["base",55,6]
							["base",56,6]
							["base",57,6]
							["base",58,6]
							["base",51,6]]}]
				"51":
					"element":[
						["base","./exice_z/shell/master/surface0051.png"]]
					"animation":[
						{"timing":"runonce"
						"pattern":[
							["base",58,6]
							["base",57,6]
							["base",56,6]
							["base",55,6]
							["base",54,6]
							["base",53,6]
							["base",52,6]
							["base",50,6]]}]
				"52":
					"element":[
						["base","./exice_z/shell/master/surface0052.png"]]
				"53":
					"element":[
						["base","./exice_z/shell/master/surface0053.png"]]
				"54":
					"element":[
						["base","./exice_z/shell/master/surface0054.png"]]
				"55":
					"element":[
						["base","./exice_z/shell/master/surface0055.png"]]
				"56":
					"element":[
						["base","./exice_z/shell/master/surface0056.png"]]
				"57":
					"element":[
						["base","./exice_z/shell/master/surface0057.png"]]
				"58":
					"element":[
						["base","./exice_z/shell/master/surface0058.png"]]
				"60":
					"element":[
						["base","./exice_z/shell/master/surface0060.png"]]
				"61":
					"element":[
						["base","./exice_z/shell/master/surface0061.png"]]
				"62":
					"element":[
						["base","./exice_z/shell/master/surface0062.png"]]
				"63":
					"element":[
						["base","./exice_z/shell/master/surface0063.png"]]
				"64":
					"element":[
						["base","./exice_z/shell/master/surface0064.png"]]
				"65":
					"element":[
						["base","./exice_z/shell/master/surface0065.png"]]
				"70":
					"element":[
						["base","./exice_z/shell/master/surface0070.png"]]
					"collision":[
						[84,103,134,136,"Head"]
						[88,144,132,174,"Face"]
						[182,191,208,251,"LeftST"]]
				"71":
					"element":[
						["base","./exice_z/shell/master/surface0071.png"]]
					"collision":[
						[95,67,157,103,"Head"]
						[104,116,158,143,"Face"]
						[106,174,155,209,"Bust"]
						[182,189,209,274,"LeftST"]]
				"72":
					"element":[
						["base","./exice_z/shell/master/surface0072.png"]]
					"collision":[
						[73,68,122,102,"Head"]
						[82,106,129,145,"Face"]
						[99,161,149,202,"Bust"]
						[179,161,213,239,"LeftST"]]
				"73":
					"element":[
						["base","./exice_z/shell/master/surface0073.png"]]
					"collision":[
						[75,70,130,106,"Head"]
						[90,111,132,143,"Face"]
						[90,188,114,216,"Bust"]
						[185,169,212,251,"LeftST"]]
				"74":
					"element":[
						["base","./exice_z/shell/master/surface0074.png"]]
					"collision":[
						[89,81,141,113,"Head"]
						[99,121,142,152,"Face"]
						[111,162,146,201,"Bust"]
						[199,139,228,221,"LeftST"]]
				"75":
					"element":[
						["base","./exice_z/shell/master/surface0075.png"]]
					"collision":[
						[107,103,161,136,"Head"]
						[113,140,157,173,"Face"]
						[131,193,169,213,"Bust"]
						[201,177,227,241,"LeftST"]]
				"76":
					"element":[
						["base","./exice_z/shell/master/surface0076.png"]]
					"collision":[
						[98,65,155,100,"Head"]
						[100,110,151,141,"Face"]
						[97,176,151,211,"Bust"]
						[190,237,216,304,"LeftST"]]
				"77":
					"element":[
						["base","./exice_z/shell/master/surface0077.png"]]
					"collision":[
						[89,81,141,113,"Head"]
						[99,121,142,152,"Face"]
						[111,162,146,201,"Bust"]
						[199,139,228,221,"LeftST"]]
				"81":
					"element":[
						["base","./exice_z/shell/master/surface0081.png"]]
				"82":
					"element":[
						["base","./exice_z/shell/master/surface0082.png"]]
				"83":
					"element":[
						["base","./exice_z/shell/master/surface0083.png"]]
				"84":
					"element":[
						["base","./exice_z/shell/master/surface0084.png"]]
				"85":
					"element":[
						["base","./exice_z/shell/master/surface0085.png"]]
				"86":
					"element":[
						["base","./exice_z/shell/master/surface0086.png"]]
				"87":
					"element":[
						["base","./exice_z/shell/master/surface0087.png"]]
				"88":
					"element":[
						["base","./exice_z/shell/master/surface0088.png"]]
				"89":
					"element":[
						["base","./exice_z/shell/master/surface0089.png"]]
hash2Array = (hash)->
	ary=[]
	(-> ary[k]=v) k,v for k,v of hash
	ary
exice_zJson.shell.master.surface = hash2Array exice_zJson.shell.master.surface

ikagaka = do ->
	isFunction = (fn)-> Object.prototype.toString.apply(fn) is "[object Function]"
	isArray = (ary)-> Object.prototype.toString.apply(ary) is "[object Array]"
	isObject = (o)-> Object.prototype.toString.apply(o) is "[object Object]"
	isImage = (img)-> Object.prototype.toString.apply(img) is "[object HTMLImageElement]"
	isNumber = (n)-> isFinite Number n
	transImg = (img)->
		canvas = $("<canvas />").attr({
			width: img.width,
			height: img.height})[0]
		ctx = canvas.getContext '2d'
		ctx.drawImage img,0,0
		try
			imgdata = ctx.getImageData 0,0,img.width,img.height
			i = 0
			[r,g,b] = [imgdata.data[0],imgdata.data[1],imgdata.data[2]]
			while i < imgdata.data.length
				imgdata.data[i+3] = 0 if r is imgdata.data[i] and g is imgdata.data[i+1] and b is imgdata.data[i+2]
				i += 4
			ctx.putImageData imgdata,0,0
		catch e
		return canvas
	copy = (parentCanvas)->
		childCanvas = $("<canvas />").attr({
			"width": parentCanvas.width,
			"height":parentCanvas.height})[0]
		ctx = childCanvas.getContext '2d'
		ctx.drawImage parentCanvas,0,0
		return childCanvas
	base = (canvas)->
		return copy.apply(@,arguments)
	overlay = (target, canvas, x, y)->
		return overlayfast.apply(@,arguments)
	overlayfast = (target,canvas,x,y)->
		ctx = target.getContext '2d'
		ctx.drawImage canvas,x,y
		return target
	draw = (target,canvas)->
		$(target).attr
			width: canvas.width
			height: canvas.height
		if target.parentNode?
			$(target.parentNode)
				.width(canvas.width)
				.height(canvas.height)
		ctx = target.getContext '2d'
		ctx.drawImage canvas,0,0
		return target
	preload = do ->
		urlImgHash = {}
		return (urlAry,callback)->
			if urlAry.length is 0
				setTimeout callback,0,[] if callback?
				return []
			i = 0
			imgAry = urlAry.map (url)->
				return urlImgHash[url] if isImage(urlImgHash[url]) and urlImgHash[url].width isnt 0
				i++
				img = urlImgHash[url] = new Image()
				img.src = localStorage[url] or url
				img.onload = img.onerror = ->
					i--
					try
						unless localStorage[url]?
							localStorage[url] = copy(img).toDataURL()
					catch e
					callback?(imgAry) if i is 0
				return img
			setTimeout callback,0,imgAry if i is 0 and callback?
			return imgAry
	doPreload = (json)->
		json.shell.master.surface.forEach (v)->
			preloadElements v.element
	preloadElements = (elementsAry,callback)->
		urlAry = elementsAry.map (v)-> return v[1]
		preload urlAry,(imgAry)->
			imagedElementsAry = elementsAry.map (v,i)->
				return [v[0],imgAry[i],v[2],v[3]]
			callback? imagedElementsAry
	composeElements = (imagedElementsAry)->
		canvas = $("<canvas />")[0]
		imagedElementsAry.forEach (v)->
			[type,img,x,y] = v
			switch type
				when "base" then canvas = base transImg img
				when "overlay" then canvas = overlay canvas,transImg(img),x,y
				when "overlayfast" then canvas = overlayfast canvas,transImg(img),x,y
		return canvas
	preloadAndComposeElements = (elementsAry,callback)->
		preloadElements elementsAry,(imagedElementsAry)->
			callback composeElements imagedElementsAry
	setElements = (target,elementsAry,callback)->
		return callback?() unless elementsAry?
		preloadAndComposeElements elementsAry,(canvas)->
			callback? draw target,canvas
	mouseEvent = (id,e,target,collisionsAry,scopeId,handler)->
		offsetX = e.offsetX or e.originalEvent.layerX
		offsetY = e.offsetY or e.originalEvent.layerY
		if isHit target,offsetX,offsetY
			e.preventDefault()
			event =
				ID: id
				Reference0: offsetX
				Reference1: offsetY
				Reference2: 0
				Reference3: scopeId
				Reference4: ""
				Reference5: 0
			delete event.Reference5 if id is "OnMouseMove"
			collisionsAry?.forEach (v)->
				[x,y,_x,_y,name] = v
				event["Reference4"] = name if (x < offsetX < _x and y < offsetY < _y) or (_x < offsetX < x and _y < offsetY < y)
			handler(event)
	setCollisions = (target,collisionsAry,scopeId,handler)->
		$(target)
			.unbind()
			.click((e)-> mouseEvent "OnMouseClick",e,@,collisionsAry,scopeId,handler)
			.dblclick((e)-> mouseEvent "OnMouseDoubleClick",e,@,collisionsAry,scopeId,handler)
			.mousedown((e)->)
			.mousemove((e)-> mouseEvent "OnMouseMove",e,@,collisionsAry,scopeId,handler)
			.mouseup((e)->)
			.bind("touchstart",do ()->
				i = 0
				(e)->
					if i++ is 1
						i = 0
						mouseEvent "OnMouseDoubleClick",e,@,collisionsAry,scopeId,handler
					setTimeout (-> i=0),500)
			.bind("touchmove",(e)-> mouseEvent "OnMouseMove",e,@,collisionsAry,scopeId,handler)
			.bind("touchend",(e)->)
			.bind("touchcancel",(e)->)
	drawCollisions = (target,collisionsAry)->
		ctx = target.getContext "2d"
		collisionsAry?.forEach (v)->
			[x,y,_x,_y,name] = v
			ctx.strokeStyle = "rgb(200, 0, 0)"
			ctx.strokeRect x,y,Math.abs(_x-x),Math.abs(_y-y)
	isHit = (target,x,y)->
		ctx = target.getContext "2d"
		try
			imgdata = ctx.getImageData 0,0,x,y
			return imgdata.data[imgdata.data.length-1] isnt 0
		return true
	sometimes = (tidAry,fn)->
		return random(tidAry,fn,2)
	rarely = (tidAry,fn)->
		return random(tidAry,fn,4)
	random = (tidAry,fn,n)->
		ms = 1
		ms++ while Math.round(Math.random() * 1000) > 1000/n
		return tidAry.push setTimeout(->
			fn()
			random(tidAry,fn,n)
		,ms*1000)
	runonce = (tidAry,fn)->
		return tidAry.push setTimeout(fn,0)
	always = (tidAry,fn)->
		return tidAry.push setTimeout(->
			fn()
			always(tidAry,fn)
		,0)
	namedIndex = 0
	namedAry = []
	class Surface
		constructor:(@_,callback)->
			@_surfaceId = @_._curSurface
			@_surfaceAry = @_._json.shell.master.surface
			@_surfaceObj = surfaceObj = @_._json.shell.master.surface[@_surfaceId]
			@_canvas = canvas = @_._surfaceCanvas
			@_timingTidAry = timingTidAry = []
			@_playingTidAryAry = playingTidAryAry = []
			@_playingPatternElementsAryAry = []
			scopeId = @_._scopeId
			raiseEvent = @_._.raiseEvent.bind @_._
			animation = @animation.bind @
			setElements canvas,surfaceObj.element,=>
				#runonce_sync
				drawCollisions canvas,surfaceObj.collision if @_._._showCollision
				callback?()
			setCollisions canvas,surfaceObj.collision,scopeId,raiseEvent
			regExp = /(\w+)(?:,\s*(\d+))?/
			surfaceObj.animation?.forEach (v,i)->
				playingTidAryAry[i] = []
				[timing,n] = regExp.exec(v.timing).slice(1)
				switch timing
					when "sometimes" then sometimes timingTidAry,->animation i
					when "rarely" then rarely timingTidAry,->animation i
					when "random" then random timingTidAry,(->animation i),n
					when "runonce" then runonce timingTidAry,->animation i
					when "always" then always timingTidAry,->animation i
		animation:(n,callback)->
			if @_._._apiLogging
				console.log "named("+@_._namedId+").scope("+@_._scopeId+").surface("+@_surfaceId+").animation("+n+")"
			surfaceObj = @_surfaceObj
			patternAry = @_surfaceObj.animation[n].pattern
			surfaceAry = @_surfaceAry
			pElmAryAry = @_playingPatternElementsAryAry
			tidAryAry = @_playingTidAryAry
			canvas = @_canvas
			i = 0
			playPattern = =>
				[type,id,delay,x,y] = patternAry[i++]
				x ?= 0
				y ?= 0
				if surfaceAry[id]?
					pElmAryAry[n] = surfaceAry[id].element.map (v)->
						[_type,url,_x,_y] = v
						_x ?= 0
						_y ?= 0
						[type,url,_x+x,_y+y]
				else if id is -1
					pElmAryAry[n] = []
				setElements canvas,(surfaceObj.element.concat pElmAryAry.reduce (tmp,v)-> tmp.concat v),=>
					drawCollisions canvas,surfaceObj.collision if @_._._showCollision
				tidAryAry[n].push setTimeout playPattern,delay*10 if patternAry[i]?
			setTimeout playPattern,0
			return
		clear:->
			if @_._._apiLogging
				console.log "named("+@_._namedId+").scope("+@_._scopeId+").surface("+@_surfaceId+").clear()"
			canvas = @_canvas
			aryAry = @_playingTidAryAry
			ary = @_timingTidAry
			$(canvas).unbind()
			aryAry.forEach (v)-> v?.forEach (v)-> clearTimeout v
			ary.forEach (v)-> clearTimeout v
		disappear:->
			if @_._._apiLogging
				console.log "named("+@_._namedId+").scope("+@_._scopeId+").surface("+@_surfaceId+").disappear()"
			canvas = @_canvas
			aryAry = @_playingTidAryAry
			ary = @_timingTidAry
			$(canvas)
				.attr({width:0,height:0})
				.unbind()
			aryAry.forEach (v)-> v?.forEach (v)-> clearTimeout v
			ary.forEach (v)-> clearTimeout v
			@_._surface = undefined
			return
	class Blimp
		constructor:(@_,callback)->
			@_blimpId = @_._curBlimp
			@_scopeId = scopeId = @_._scopeId
			@_balloonDiv = @_._balloonDiv
			@_blimpCanvas = blimpCanvas = @_._blimpCanvas
			@_textDiv = @_._textDiv
			preload ["./ssp/balloons0.png","./ssp/balloons1.png","./ssp/balloonk0.png","./ssp/balloonk1.png"],(ary)=>
				target = draw blimpCanvas,transImg(ary[(if scopeId is 0 then 0 else 2)])
				#$(@_balloonDiv).width(target.width).height(target.height)
				$(@_textDiv).width(target.width-30).height(target.height-20)
		talk:(chr)->
			@_._curBlimp = @_blimpId
			$(@_balloonDiv).css display: "block"
			@_textDiv.scrollTop = 999
			if @_._._apiLogging
				console.log "named("+@_._namedId+").scope("+@_._scopeId+").talk(\""+chr+"\")"
			$(@_balloonDiv).css visibility: "visible"
			$(@_textDiv).append chr
			return
		br:->
			if @_._._apiLogging
				console.log "named("+@_._namedId+").scope("+@_._scopeId+").blimp("+@_blimpId+").br()"
			$(@_textDiv).append "<br />"
			return
		clear:->
			if @_._._apiLogging
				console.log "named("+@_._namedId+").scope("+@_._scopeId+").blimp("+@_blimpId+").clear()"
			$(@_textDiv).html ""
		disappear:->
			if @_._._apiLogging
				console.log "named("+@_._namedId+").scope("+@_._scopeId+").blimp("+@_blimpId+").disappear()"
			$(@_textDiv).html ""
			$(@_blimpCanvas)
				.attr({width:0,height:0})
				.unbind()
			@_._blimp = undefined
			return
	class Scope
		constructor:(@_)->
			@_namedId = namedId = @_._namedId
			@_scopeId = scopeId = @_._curScope
			@_json = @_._json
			@_curSurface = -1
			@_curBlimp = -1
			@_scopeDiv = $("<div class=\"scope\" />")
				.addClass("scope"+scopeId)
				.css(
					bottom: "0px"
					right: (scopeId*240)+"px"
					visibility: "hidden")
				.append("<canvas class=\"surface\" width=\"0\" height=\"0\" />")
				.append($("<div class=\"balloon\" />")
					.css(
						display: "none"
						top:"60px"
						left: "-300px")
					.append("<canvas class=\"blimp\" width=\"0\" height=\"0\" />")
					.append("<div class=\"text\" style=\"margin:10px;margin-right:30px;overflow-y:scroll;white-space:pre;white-space:pre-wrap;white-space:pre-line;word-wrap:break-word;\" />"))
				.appendTo(@_._namedDiv)
				.click => $(@_scopeDiv).appendTo(@_._namedDiv)
			@_surfaceCanvas = $("#named"+namedId+">.scope"+scopeId+">.surface")[0]
			@_balloonDiv = $("#named"+namedId+">.scope"+scopeId+">.balloon")[0]
			@_blimpCanvas = $("#named"+namedId+">.scope"+scopeId+">.balloon>.blimp")[0]
			@_textDiv = $("#named"+namedId+">.scope"+scopeId+">.balloon>.text")[0]
			@_surface = null
			@_curBlimp = 0
			@_blimp = new Blimp @
			@_curBlimp = -1
		surface:(n,callback)->
			unless n is @_curSurface
				@_curSurface = Number n if isNumber n
				if n is -1
					$(@_scopeDiv).css visibility: "hidden" 
				else if @_json.shell.master.surface[n]?
					$(@_scopeDiv).css visibility: "visible"
					@_surface?.clear()
					@_surface = new Surface @,callback
				if @_._apiLogging and n?
					console.log "named("+@_namedId+").scope("+@_scopeId+").surface("+@_curSurface+")"
			else setTimeout (->callback?()),0
			return @_surface
		blimp:(n,callback)->
			unless n is @_curBlimp
				@_curBlimp = Number n if isNumber n
				if @_curBlimp is -1
					$(@_balloonDiv).css display: "none"
					if @_._apiLogging
						console.log "named("+@_namedId+").scope("+@_scopeId+").blimp("+@_curBlimp+")"
				else if @_json.shell.master.surface[n]?
					$(@_balloonDiv).css display: "block"
					@_blimp?.disappear()
					@_blimp = new Blimp @,callback
					if @_._apiLogging
						console.log "named("+@_namedId+").scope("+@_scopeId+").blimp("+@_curBlimp+")"
			else setTimeout (->callback?()),0
			return @_blimp
		disappear:->
			if @_._apiLogging
				console.log "named("+@_namedId+").scope("+@_scopeId+").disappear()"
			@_surface.disappear()
			@_blimp.disappear()
			$(@_scopeDiv).remove()
			@_._scopeAry[@_scopeId] = undefined
			return
	class Named
		constructor:(@_json,args)->
			callback = args[args.length-1] if isFunction args[args.length-1]
			if isObject args[0]
				@_eventLogging = 		if args[0].eventLogging? 		then args[0].eventLogging else false
				@_apiLogging = 			if args[0].apiLogging? 			then args[0].apiLogging else false
				@_secondChangeLogging = if args[0].secondChangeLogging? then args[0].secondChangeLogging else false
				@_mouseMoveLogging = 	if args[0].mouseMoveLogging? 	then args[0].mouseMoveLogging else false
				@_preload = 			if args[0].preload? 			then args[0].preload else false
				@_showCollision = 		if args[0].showCollision? 		then args[0].showCollision else false
			@_namedId = namedId = namedIndex
			namedAry[namedIndex++] = @
			@_curScope = 0
			@_scopeAry = []
			@_scriptTidAry = []
			@_eventHandlerHash = {}
			@_talking = false
			@_namedDiv = $("<div />")
				.attr({id:"named"+namedId})
				.addClass("named")
				.append($("<style scoped />")
					.html("#named"+namedId+" *{position:absolute;border:none;margin:0px;padding:0px;-webkit-user-select:none;-webkit-tap-highlight-color:transparent;}"))
				.appendTo("body")
			@_scopeAry[@_curScope] = new Scope @
			if @_apiLogging
				console.log "named(#{namedId})"
			@_secondChangeTid = setInterval (=>
				e =
					ID: "OnSecondChange"
					Reference0: 0
					Reference1: 0
					Reference2: 0
					Reference3: 0
				@raiseEvent e
			),1000
			setTimeout (=>
				callback?()
				e =
					ID: "OnBoot"
					Reference0: "master"
				@raiseEvent e
			),0
			doPreload(@_json) if @_preload
		scope:(n)->
			return @_scopeAry[@_curScope] unless isNumber n
			@_curScope = Number n
			if @_apiLogging
				console.log "named("+@_namedId+").scope("+@_curScope+")"
			@_scopeAry[@_curScope] ?= new Scope @
			$(@_scopeAry[@_curScope]._scopeDiv).appendTo(@_namedDiv)
			return @_scopeAry[@_curScope]
		playScript:(script,callback)->
			@breakScript()
			@_talking = true
			namedId = @_namedId
			scope = @scope.bind @
			tidAry = @_scriptTidAry
			script ?= ""
			if @_apiLogging
				console.log "named("+@_namedId+").playScript(\""+script+"\")"
			regExpHash =
				yy: /^\\\\/
				y0: /^\\0/
				y1: /^\\1/
				yp: /^\\p\[(\d+)\]/
				ys: /^\\s\[([^\]]+)\]/
				yi: /^\\i\[(\d+)\]/
				ywN: /^\\w(\d+)/
				y_w: /^\\_w\[(\d+)\]/
				yc: /^\\c/
				yn: /^\\n/
				ynhalf: /^\\n\[half\]/
				ye: /^\\e/
			isCallcc = false
			callcc = (rescript,talkWait)->
				isCallcc = true
				return -> 
					isCallcc = false
					tidAry.push setTimeout playingScript,talkWait,rescript
			playingScript = (script)=>
				return tidAry.push setTimeout (=>
					@breakScript()
					callback?(tidAry)),10000 if script.length is 0
				talkWait = 0
				switch true
					when regExpHash.y0.test script
						rescript = script.substr 2
						scope(0)
					when regExpHash.y1.test script
						rescript = script.substr 2
						scope(1)
					when regExpHash.yp.test script
						n = Number regExpHash.yp.exec(script)[1]
						rescript = script.replace regExpHash.yp,""
						scope(n)
					when regExpHash.ys.test script
						n = Number regExpHash.ys.exec(script)[1]
						rescript = script.replace regExpHash.ys,""
						scope().surface(n,callcc(rescript,talkWait))
					when regExpHash.yi.test script
						n = Number regExpHash.yi.exec(script)[1]
						rescript = script.replace regExpHash.yi,""
						scope().surface().animation(n,callcc(rescript,talkWait))
					when regExpHash.ywN.test script
						talkWait = Number regExpHash.ywN.exec(script)[1]*100
						rescript = script.replace regExpHash.ywN,""
						if @_apiLogging
							console.log "named(#{namedId}).wait(\"#{talkWait}\")"
					when regExpHash.y_w.test script
						talkWait = Number regExpHash.y_w.exec(script)[1]
						rescript = script.replace regExpHash.y_w,""
						if @_apiLogging
							console.log "named(#{namedId}).wait(\"#{talkWait}\")"
					when regExpHash.yc.test script
						rescript = script.substr 2
						scope().blimp().clear()
					when regExpHash.ynhalf.test script
						rescript = script.substr 8
						scope().blimp().br()
					when regExpHash.yn.test script
						rescript = script.substr 2
						scope().blimp().br()
					when regExpHash.ye.test script
						rescript = ""
						if @_apiLogging
							console.log "named(#{namedId}).yen-e()"
					when regExpHash.yy.test script
						rescript = script.substr 2
						scope().blimp().talk("\\")
					else
						chr = script.substr 0,1
						rescript = script.substr 1
						scope().blimp().talk(chr)
						talkWait = 80
				return tidAry.push setTimeout playingScript,talkWait,rescript unless isCallcc
			playingScript(script)
			return
		breakScript:->
			@_scriptTidAry.forEach (v)-> clearTimeout v
			@_scopeAry.forEach (v)-> v.blimp(-1).clear()
			@_talking = false
			return
		raiseEvent:(e)->
			if @_apiLogging and ((if e.ID is "OnSecondChange" then @_secondChengeLogging else true) and (if e.ID is "OnMouseMove" then @_mouseMoveLogging else true))
				console.log "named("+@_namedId+").raiseEvent("+e.ID+")"
			if @_eventLogging and ((if e.ID is "OnSecondChange" then @_secondChengeLogging else true) and (if e.ID is "OnMouseMove" then @_mouseMoveLogging else true))
				console.log "--- Request ---"
				console.log "GET SHIORI/3.0"
				console.log "Charset: UTF-8"
				console.log "#{k} :#{v}" for k,v of e
				console.log "--- Operation ---"
			script = @_eventHandlerHash[e.ID]?(e)
			if @_eventLogging and ((if e.ID is "OnSecondChange" then @_secondChengeLogging else true) and (if e.ID is "OnMouseMove" then @_mouseMoveLogging else true))
				if script?
					console.log "status code : 200"
					console.log "--- Response ---"
					console.log "value="+script
				else
					console.log "status code : 204"
					console.log "--- Response ---"
				console.log ""
			if script?
				@breakScript()
				setTimeout (=>@playScript script),0
			return
		on:(id,handler)->
			if @_apiLogging
				console.log "named("+@_namedId+").on(\""+id+"\")"
			@_eventHandlerHash[id] = handler
			return
		vanish:->
			if @_apiLogging
				console.log "named("+@_namedId+").vanish()"
			@breakScript()
			clearTimeout @_secondChangeTid
			@_scopeAry.forEach (v)-> v.disappear()
			$("#named"+@_namedId).remove()
			namedAry[@_namedId] = undefined
			return
	return {
		named:(n)-> namedAry[n]
		createNamed:(json,args...)-> new Named json,args
	}

$ ->
	choiceByAry = (ary)-> ary[Math.round(Math.random()*(ary.length-1))]
	exice_z = ikagaka.createNamed exice_zJson,{
		apiLogging:false
		eventLogging:false
		secondChangeLogging:false
		mouseMoveLogging:false
		showCollision:false
		preload:false}
	exice_z.on "OnBoot",(e)->
		choiceByAry [
			"\\1\\s[10]\\0\\s[43]\\w8\\w6\\w6イクサイスゼロ…\\w3\\s[77]参上っ！\\w6\\_w[72]\\n\\n[half]\\_w[6]\\1元気の良い事で。\\_w[48]\\e"
			"\\1\\s[10]\\0\\s[20]…\\w3…\\w3。\\_w[18]\\w8\\w6\\n\\n[half]\\_w[6]\\1剣爺殿、\\_w[24]ゼロは何処ですかな？\\w4\\_w[60]\\n\\n[half]\\_w[6]\\0「眠いって言っといて」との事ですよ。\\_w[108]\\w6\\n\\n[half]\\_w[6]\\1…\\w3…\\w3。\\_w[18]\\w8\\n\\s[11]たわけ！出て来んかっ！\\w8\\_w[66]\\n\\n[half]\\_w[6]\\0\\c\\s[4]…\\w3うー…\\w3。\\_w[30]\\w4ヒドイな～。\\_w[36]\\e"
			"\\1\\s[10]\\0\\s[51]\\w6\\w6\\s[5]はい、\\_w[18]お呼びですかご主人様っ！\\w6\\_w[72]\\n\\n[half]\\_w[6]\\1似合わない事はするんじゃないぞ？\\w6\\_w[96]\\n\\n[half]\\_w[6]\\0\\s[7]むー。\\_w[18]\\e"
			"\\1\\s[10]\\0\\s[43]\\w8\\w8\\w8\\s[0]到着ー。\\_w[24]\\w6\\n\\s[5]ユーザーさん、\\_w[18]こんちわー。\\_w[36]\\e"
			"\\1\\s[10]\\1ワシ参上。\\_w[30]\\w4\\n\\n[half]\\_w[6]\\0\\s[4]本物の年寄りに言われても…\\w3。\\_w[84]\\w4\\n\\n[half]\\_w[6]\\1萌えたじゃろ？\\w4\\_w[42]\\n\\n[half]\\_w[6]\\0\\s[0]いいや。\\_w[24]崩え。\\_w[18]\\w4\\n\\n[half]\\_w[6]\\1何じゃそれは…\\w3。\\_w[48]\\e"
			
		]
	iRandamTalk = 10
	exice_z.on "OnSecondChange",(e)->
		if iRandamTalk <= 0
			iRandamTalk++ while Math.round(Math.random() * 500) > 1
			return choiceByAry [
				"\\0\\s[0]\\1\\s[10]\\1ゼロさんや。\\_w[36]\\w6\\n\\n[half]\\_w[6]\\0何？\\w6\\_w[12]\\n\\n[half]\\_w[6]\\1暑くないのかの？\\w6\\_w[48]\\n\\n[half]\\_w[6]\\0うん、\\_w[18]暑くないよ。\\_w[36]\\w2寒くも無いね。\\_w[42]\\w6\\nこの服は着用者も快適に保ってくれるんだよ。\\_w[126]\\w6\\n\\n[half]\\_w[6]\\1ほほー。\\_w[24]\\w6\\n\\n[half]\\_w[6]\\0エアコンが寒いとか暑いとか言ってたら変でしょ？\\w6\\_w[138]\\n\\n[half]\\_w[6]\\1確かに商品イメージにはかかわるかな？\\_w[108]\\n\\_w[654]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0そういえば、\\_w[36]荻月ってどうして年寄り言葉で話すの？\\w6\\_w[108]\\n\\n[half]\\_w[6]\\1魂がジジイじゃからな。\\_w[66]\\w6\\n\\n[half]\\_w[6]\\0\\_a[【ジジイの魂】]【ジジイの魂】\\_a？\\_w[48]\\n\\_w[288]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0実は料理とか鉱石精製とか趣味だよ。\\_w[102]\\w6\\n\\n[half]\\_w[6]\\1壊滅的にヘタクソじゃが。\\_w[72]\\w6\\n\\n[half]\\_w[6]\\0\\s[7]うるさいなー。\\_w[42]\\n\\_w[246]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0伝説の武器なんて、\\_w[54]世界の数だけあるんだから。\\_w[78]\\w6\\n…\\w3私のところに一本ぐらいあってもいいよね？\\w8\\_w[126]\\n\\n[half]\\_w[6]\\1そんな事言われてものぉ。\\_w[72]\\w8\\n\\n[half]\\_w[6]\\0という訳で、\\_w[36]\\s[5]アロンダイトカムヒアー！\\w6\\_w[108]\\n…\\w3なんてね。\\_w[36]\\w8\\n\\n[half]\\_w[6]\\1…\\w3危ないからコイツだけには渡したくないっていう場合もあるわけで。\\_w[192]\\w8\\n\\n[half]\\_w[6]\\0\\s[0]…\\w3失礼、\\_w[24]今何て。\\_w[24]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0燃料が増える…\\w3？　\\_w[54]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0陶芸にいそしむ荻月。\\_w[60]\\w4\\n\\n[half]\\_w[6]\\1いそしみません。\\_w[48]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0銃を持った相手に、\\_w[54]\\n\\s[7]動くなッ！\\w4\\_w[30]\\n動かないと撃つぞッ！\\w4\\_w[60]\\n\\n\\s[0]…\\w3って言われたらどうする？\\w6\\_w[78]\\n\\n[half]\\_w[6]\\1…\\w3ま、\\_w[18]動くかのぅ。\\_w[36]\\w6\\n\\n[half]\\_w[6]\\0ほぅ。\\_w[18]そのココロは？\\w6\\_w[42]\\n\\n[half]\\_w[6]\\1「動くな」の方では撃つとは言っていないからの。\\_w[138]\\nそれに動いてれば避けられるかもしれんじゃろ。\\_w[132]\\w4\\n\\n[half]\\_w[6]\\0ははぁ。\\_w[24]考えたね。\\_w[30]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0わたしの着てるこの服は、\\_w[72]\\_a[【イクサイスユニット】]【イクサイスユニット】\\_aって言うんだ。\\_w[108]\\w6\\n着用者に空調機能を付与するよ。\\_w[90]\\w6\\nユニットを着用して、\\_w[60]空調機能を自分の能力として使える状態になっている人のことを\\_a[【イクサイス】]【イクサイス】\\_aと呼ぶよ。\\_w[246]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0今精霊銀でこけしを作るのが流行っているんだよ。\\_w[174]\\w6\\n\\n[half]\\_w[6]\\1ほぉ。\\_w[18]どのくらい流行っておるのじゃ？\\w6\\_w[90]\\n\\n[half]\\_w[6]\\0\\s[5]全米Ｎｏ.１ＨＩＴ！ぐらいかな？\\w6\\_w[93]\\n\\n[half]\\_w[6]\\1…\\w3説得力が微塵も無くなったぞ。\\_w[90]\\n\\_w[471]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0荻月って、\\_w[30]学術っぽく言うとどんな分類になるの？\\w6\\_w[108]\\n\\n[half]\\_w[6]\\1有機微小精密機械集合式霊子付与擬似生命体…\\w3ってところかのぉ。\\_w[180]\\w8\\n適当じゃが。\\_w[36]\\w6\\n\\n[half]\\_w[6]\\0\\s[4]意味不明デス。\\_w[42]\\w6\\n\\n[half]\\_w[6]\\1有機ナノマシン集合体に、\\_w[72]魂をくっつけた生物です、\\_w[72]って感じじゃな。\\_w[48]\\w8\\n魂付与は錬金術的なものに頼ってるらしいから、\\_w[132]かなり正体不明じゃと思うがな。\\_w[90]\\w8\\n\\n[half]\\_w[6]\\0\\s[0]ＡＩじゃなかったんだ？\\w6\\_w[66]\\n\\n[half]\\_w[6]\\1ああ。\\_w[18]他の\\_a[【サポートユニット】]【サポートユニット】\\_aの連中は皆ＡＩじゃがな。\\_w[144]\\w6\\nお前さんの珍妙な能力に付き合うのはＡＩじゃ大変らしいゾ。\\_w[168]\\w6\\n\\n[half]\\_w[6]\\0\\s[7]誰が珍妙だってー？\\w6\\_w[54]\\n\\_w[1356]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0県名を技名として叫ぶとき、\\_w[78]埼玉県が一番語呂が良いよね。\\_w[84]\\w4\\n埼玉拳ーって。\\_w[42]\\w4何でだろうね？\\w4\\_w[42]\\n\\n[half]\\_w[6]\\1さぁのぅ…\\w3。\\_w[36]\\w4\\n\\n[half]\\_w[6]\\0…\\w3砕玉拳…\\w3？\\w4\\_w[36]\\n\\n[half]\\_w[6]\\1…\\w3！\\_w[12]\\n\\s[13]何を言い出すんじゃ！はしたない！\\_w[96]\\n\\n[half]\\_w[6]\\0\\s[2]え？え？…\\w3なんで？\\_w[54]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0\\_a[【イクサイス】]【イクサイス】\\_aの持つ基本的な能力は、\\_w[108]「空調機能」と「身体強化」だよ。\\_w[96]\\w6\\n\\n「空調機能」は文字通りエアコンとしての機能で、\\_w[138]着用者およびその周りの人に快適な空間をもたらす事が可能になるよ。\\_w[192]\\w8\\n\\n「身体強化」は、\\_w[48]外界の危険から着用者の安全を守るためのもので、\\_w[138]着用者の運動神経や防御力を着ている間ずっと向上させ続けるよ。\\_w[180]\\w8\\n\\n[half]\\_w[6]\\1どのくらい強くなるんじゃったかな？\\w6\\_w[102]\\n\\n[half]\\_w[6]\\0運動神経は、\\_w[36]通常時１００の力を出せる人が１２０の力を出せるようになるくらい。\\_w[192]\\w8\\n防御力は、\\_w[30]車にはねられても一応死なないくらいかな。\\_w[120]\\w6\\n\\n[half]\\_w[6]\\1凄い事は凄いが…\\w3微妙なラインじゃな。\\_w[108]\\e"
				"\\0\\s[0]\\1\\s[10]\\0ただ、\\_w[18]私の着ているユニットは、\\_w[72]\\_a[【Ｚ式】]【Ｚ式】\\_aっていうプロトタイプなんだ。\\_w[108]\\w8\\n一般に出回ってる量産型とは若干性能が違うよ。\\_w[132]\\w8\\n\\n[half]\\_w[6]\\1アレで若干と仰りマスカ。\\_w[72]\\w6\\n\\n[half]\\_w[6]\\0\\s[9]…\\w3露骨に戦闘能力あるもんね。\\_w[84]\\w6\\n\\n[half]\\_w[6]\\1どちらかというと戦闘能力の方がメインで、\\_w[120]エアコンはオマケに見えるんじゃが…\\w3。\\_w[108]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0病院の空気がたまらなく好き。\\_w[84]消毒用アルコールの匂いも好き。\\_w[90]\\w6\\n\\n[half]\\_w[6]\\1微妙に危なっかしい趣味ですな。\\_w[90]\\w8\\n\\n[half]\\_w[6]\\0ほら、\\_w[18]私\\_a[【アルビノ】]【アルビノ】\\_aだし、\\_w[60]イクサイスになる前は外出できない体だったでしょ？\\w8\\_w[144]\\n検査を受けに病院に行くのは、\\_w[84]唯一昼間に外出する機会だったから特別な印象があるんだ。\\_w[162]\\w6\\n\\n[half]\\_w[6]\\1ふむ…\\w3。\\_w[24]\\w6\\n\\n[half]\\_w[6]\\0\\xちなみに。\\_w[30]\\w6\\n病院に行く際に、\\_w[48]後部座席に光が入らないようにした車に乗ってたんだけど…\\w3。\\_w[168]\\w8\\nこれも変な気分がするから好きだったよ。\\_w[114]\\w6\\n\\n[half]\\_w[6]\\1それは変な子でしたね。\\_w[66]\\w6\\n今でも変な子ですが。\\_w[60]\\w6\\n\\n[half]\\_w[6]\\0\\s[76]精密射撃！\\1\\s[17]うわああぁ――！\\_w[150]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0この前、\\_w[24]変わった事に誘われたよ。\\_w[72]\\w8\\n\\n[half]\\_w[6]\\1ホホゥ。\\_w[24]どんな？\\w6\\_w[24]\\n\\n[half]\\_w[6]\\0格好よくきめたお兄さんだったんだけど、\\_w[114]\\w6\\n「お嬢さん、\\_w[36]一緒にお茶でも育てませんか？」だって。\\_w[114]\\w8\\n\\n[half]\\_w[6]\\1…\\w3…\\w3。\\_w[18]\\w6\\n内容はともかく、\\_w[48]知らない人にほいほいついて行っちゃダメじゃぞ。\\_w[138]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0そ ん な ば か な っ ！\\w8\\_w[69]\\n…\\w3。\\_w[12]\\w8　\\_w[6]\\n…\\w3…\\w3。\\_w[18]\\w8\\n\\n[half]\\_w[6]\\1…\\w3？\\w8　\\_w[18]\\n\\n[half]\\_w[6]\\0\\s[9]七言絶句…\\w3なんて。\\_w[54]\\w6\\n\\n[half]\\_w[6]\\1４点。\\_w[18]\\n\\n[half]\\_w[6]\\0\\s[4]がーん…\\w3。\\_w[30]　\\_w[6]\\e"
				"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0極端に暑い時期と極端に寒い時期が別々にあるから、\\_w[144]\\w4エアコンが苦労する事になるんだよ。\\_w[102]\\w6\\n気温、\\_w[18]一年中平均的ならイイのにね。\\_w[84]\\w8\\n\\n[half]\\_w[6]\\1氷雪気候とか熱帯雨林気候のようにか？\\w6\\_w[108]\\n\\n[half]\\_w[6]\\0\\s[4]いや、\\_w[18]そうじゃなくって…\\w3。\\_w[60]\\e"
			]
		else
			iRandamTalk--
			return
	i0BustDoubleClick = 0
	i0FaceDoubleClick = 0
	exice_z.on "OnMouseDoubleClick",(e)->
		return if e.Reference3 is 0
			if e.Reference4 is "Head"
				"\\1\\s[10]\\0\\s[5]御用ですか？\\_w[36]\\n\\n\\q[風力調整,風力調整風力調整1]\\n\\q[イクサイス解説書,イクサイス解説書イクサイス解説書2]\\n\\q[自己紹介,自己紹介自己紹介3]\\n\\q[用語解説,用語解説用語解説4]\\n\\q[俺の名を言ってみろ！,俺の名を言ってみろ！俺の名を言ってみろ！5]\\n\\q[次のページ,次のページ次のページ6]\\n\\n\\q[呼んだだけ,呼んだだけ呼んだだけ7]\\e"
			else if e.Reference4 is "HeadEX"
				"\\1\\s[10]\\0\\s[32]…\\w3！！\\w8\\_w[18]\\nううぅ、\\_w[24]\\w4なんで叩くかな…\\w3。\\_w[54]\\w6\\n\\s[3]\\e"
			else if e.Reference4 is "Face"
				ary = [
					"\\1\\s[10]\\0\\s[1]ぅ。\\_w[12]\\e"
					"\\1\\s[10]\\0\\s[2]痛いってー。\\_w[36]\\e"
					"\\1\\s[10]\\0\\s[3]肌を触られるの弱いんだよー。\\_w[84]\\e"
					"\\1\\s[10]\\0\\s[4]うぅー。\\_w[24]家電虐待だー。\\_w[42]\\w6\\n\\n[half]\\_w[6]\\1しれっと人権放棄するようなこと言わない。\\_w[120]\\w6\\n\\n[half]\\_w[6]\\0じゃ、\\_w[18]幼女虐待だー。\\_w[42]\\w6\\n\\n[half]\\_w[6]\\1「聞こえ」は恐ろしく悪くなったな…\\w3。\\_w[108]\\e"
				]
				if i0FaceDoubleClick is ary.length then i0FaceDoubleClick = 0
				ary[i0FaceDoubleClick++]
			else if e.Reference4 is "Bust"
				ary = [
					"\\0\\s[0]\\1\\s[10]\\0つついた…\\w3？\\_w[36]\\e"
					"\\0\\s[4]…\\w3つついたね？\\w8\\_w[42]\\n…\\w3二回もつついたね？\\w8\\_w[60]\\n\\s[7]主任にもつつかれたこと無いのにっ！\\w8\\_w[102]\\n\\n[half]\\_w[6]\\1\\s[12]そんな叔父、\\_w[36]問題です！\\_w[30]\\e"
					"\\1\\s[10]\\0\\s[7]～～～～～…\\w3！\\w6\\_w[42]\\n\\n[half]\\_w[6]\\1その服…\\w3\\w4衝撃には強いんじゃが。\\_w[90]\\w4\\n触られるだけなら感触はそのまま伝わるらしいぞ。\\_w[138]\\w8\\n\\n[half]\\_w[6]\\0余計な事言わないの！\\_w[60]\\e"
					"\\1\\s[10]\\0\\s[4]そろそろ身の危険を感じて良いですか。\\_w[108]\\e"
					"\\0\\s[73]ユーザーさん、\\_w[18]成敗ッ！\\w6\\_w[24]\\n\\n[half]\\_w[6]\\1\\s[17]巻き込まれ！\\_w[36]\\e"
				]
				if i0BustDoubleClick is ary.length then i0BustDoubleClick = 0
				ary[i0BustDoubleClick++]
			else if e.Reference4 is "LeftST"
				"\\0\\s[0]\\1\\s[10]\\0更新履歴だよ。\\_w[42]\\n\\n2005/12/27   報告を受けた誤記誤字脱字等を修正。\\_w[141]\\n　　　　　　 サイト移転につきＵＲＬの変更も。\\_w[135]\\n　　　　　　 ネットワーク更新をひとまず解除。\\_w[135]\\n2003/6/23　　バグを少し修正。\\_w[87]\\n2003/6/22　　ゴースト「イクサイス・\\w3ゼロ・\\w3」\\_w[129]\\n　　　　　　 正式版リリース。\\_w[87]\\e"
			else choiceByAry []
		else if e.Reference3 is 1
			choiceByAry [
				"\\0\\s[0]\\1\\s[13]そのツボはっ！\\_w[42]\\e"
				"\\1\\s[15]あらやだ！\\_w[30]\\n\\n[half]\\_w[6]\\0\\s[26]…\\w3？！\\_w[18]\\e"
				"\\0\\s[0]\\1\\s[11]キシャー！\\_w[30]\\e"
				"\\0\\s[0]\\1\\s[12]ワシに触れると…\\w3\\w4爆発しますぞ？\\w4\\_w[90]\\n\\n[half]\\_w[6]\\0そんな事言う人機雷です。\\_w[72]\\w4\\n\\n[half]\\_w[6]\\1\\s[14]ベタネタかつお寒いですね。\\_w[78]\\w4\\n\\n[half]\\_w[6]\\0冷房だからいんだよ。\\_w[60]\\e"
				"\\0\\s[0]\\1\\s[17]ウボァー！\\_w[30]\\e"
				
			]
		else choiceByAry []
	i0HeadMove = 0
	x0HeadMove = 0
	t0HeadMove = 0
	t0HeadEXMove = 0
	x0HeadEXMove = 0
	x0FaceMove = 0
	t0FaceMove = 0
	i0BustMove = 0
	x0BustMove = 0
	t0BustMove = 0
	exice_z.on "OnMouseMove",(e)->
		return if e.Reference3 is 0
			if e.Reference4 is "Head"
				x0HeadMove++
				clearTimeout t0HeadMove
				t0HeadMove = setTimeout (->x0HeadMove=0),5000
				if x0HeadMove > 100
					x0HeadMove = 0
					ary = [
						"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0なでられてる？\\_w[42]\\e"
						"\\1\\s[10]\\0\\s[5]帽子が邪魔で良くわからないな…\\w3。\\_w[96]\\e"
						"\\1\\s[10]\\0\\s[1]…\\w3。\\_w[12]\\e"
						"\\1\\s[10]\\0\\s[5]そうだ、\\_w[24]ちょっと帽子とるから…\\w3ね？\\s[30]\\_w[78]\\e"
					]
					if i0HeadMove is ary.length then i0HeadMove = 0
					ary[i0HeadMove++]
			else if e.Reference4 is "HeadEX"
				x0HeadEXMove++
				clearTimeout t0HeadEXMove
				t0HeadEXMove = setTimeout (->x0HeadEXMove=0),5000
				if x0HeadEXMove > 100
					x0HeadEXMove = 0
					choiceByAry [
						"\\1\\s[10]\\0\\s[31]えへへっ。\\_w[30]\\n\\n[half]\\_w[6]\\1猫みたいなヤツじゃなー。\\_w[72]\\e"
						"\\1\\s[10]\\0\\s[31]えへへ…\\w3。\\_w[30]\\n\\n[half]\\_w[6]\\1こうしてるとカワイイんじゃがのぉ…\\w3。\\_w[108]\\n\\n[half]\\_w[6]\\0…\\w3今は聞き流してあげる。\\_w[72]\\e"
					]
			else if e.Reference4 is "Face"
				x0FaceMove++
				clearTimeout t0FaceMove
				t0FaceMove = setTimeout (->x0FaceMove=0),5000
				if x0FaceMove > 100
					x0FaceMove = 0
					"\\1\\s[10]\\0\\s[3]うう、\\_w[18]顔筋なぞらないでよー。\\_w[66]\\e"
			else if e.Reference4 is "Bust"
				x0BustMove++
				clearTimeout t0BustMove
				t0BustMove = setTimeout (->x0BustMove=0),5000
				if x0BustMove > 100
					x0BustMove = 0
					ary = [
						"\\0\\s[0]\\1\\s[10]\\1\\n[half]\\_w[6]\\0…\\w3…\\w3。\\_w[18]\\e"
						"\\0\\s[0]\\1\\s[10]\\0も\\_w[6]\\n\\n[half]\\_w[6]\\1揉むほど無いですぞ。\\_w[60]\\n\\n[half]\\_w[6]\\0\\s[4]先に言わないでー。\\_w[54]\\e"
						"\\1\\s[10]\\0\\s[4]それでも揉むんだね…\\w3。\\_w[66]\\e"
						"\\1\\s[10]\\0\\s[4]\\n\\1少しは何か感じるか？\\_w[60]\\n\\n[half]\\_w[6]\\0黙れー。\\_w[24]\\n\\n[half]\\_w[6]\\1\\s[17]び！\\_w[12]\\e"
						"\\0\\s[0]\\0身にかかる火の粉は…\\w3、\\_w[66]\\n\\s[70]振り払わなければなりますまい！\\_w[90]\\n\\n[half]\\_w[6]\\1\\s[14]ノエル！\\_w[24]\\e"
					]
					if i0BustMove is ary.length then i0BustMove = 0
					ary[i0BustMove++]
			else if e.Reference4 is "LeftST"
			else choiceByAry []
		else if e.Reference3 is 1
		else choiceByAry []
	$("#sakurafield").submit ->
		ikagaka.named(0).playScript $('#sakurascript').val()
</script>
</head>
<body>
<form action="#sakurafield" id="sakurafield" style="margin:20px;margin-top:60px">
	<label>Send: <input id="sakurascript" value="" type="text" /></label><input type="submit" />
</form>
</body>
</html>